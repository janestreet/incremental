---
title: Incremental tutorial, Part 2; Dynamic computations with bind
uuid: 7908ab37-d8c2-3857-92c5-f9b3c88387ba
---

The computations we've contemplated thus far have been static, which
is to say that the basic shape of the computation doesn't change.  For
example, our last example in the previous section was summing together
a fixed set of inputs.  The individual values could change, but not
which inputs are included.

Bind, it turns out, provides us with one simple mechanism for making
computations more dynamic.  To illustrate how it works, let's consider
two different ways of expressing an if statement, one using `map`, one
using `bind`.

```ocaml
# open Core
# module Incr = Incremental.Make ()
module Incr : Incremental.S
# open Incr.Let_syntax;;

# let if_with_map c t e =
    let%map c = c and t = t and e = e in
    if c then t else e
val if_with_map : bool Incr.t -> 'a Incr.t -> 'a Incr.t -> 'a Incr.t = <fun>
```

This creates a computation graph that looks something like this.

```
  +---+
  | c |-.
  +---+  \
  +---+   '->+----+
  | t |----->| if |
  +---+   .->+----+
  +---+  /
  | e |-'
  +---+
```

In other words, the condition, then and else branch are all hooked up,
and if any input changes, the if will be re-evaluated.  But this
really overapproximates the necessary dependencies, since, when the
condition `c` is true, the `if` node should only depend on the `t`,
not on the `e` node; and the reverse when `c` is false.  In other
words, the dependency structure should depend dynamically on the data.

We can write that using `bind` as follows.

```ocaml
# let if_with_bind c t e =
    let%bind c = c in
    if c then t else e
val if_with_bind : bool Incr.t -> 'a Incr.t -> 'a Incr.t -> 'a Incr.t = <fun>
```

Here, we bind on `c`, and pick the corresponding incremental depending
on the value of `c`.  The dependency structure now will look like one
of the following two pictures:

```
    if c is true             if c is false

   +---+                    +---+
   | c |-.                  | c |-.
   +---+  \                 +---+  \
   +---+   '->+----+        +---+   '->+----+
   | t |----->| if |        | t |      | if |
   +---+      +----+        +---+   .->+----+
   +---+                    +---+  /
   | e |                    | e |-'
   +---+                    +---+
```

That's the very simplest form of dynamism, where the only thing that's
changing dynamically is a single dependency. But `bind` lets you do more
than just change dependencies; you can also create new computations
with new nodes.

Here's an example of this in action. First, let's bring back the
function for summing together a list of incrementals from the previous
part.
|}]

```ocaml
# let incr_list_sum l =
    match List.reduce_balanced l ~f:(Incr.map2 ~f:( +. )) with
    | None -> return 0.
    | Some x -> x
val incr_list_sum : float Incr.t list -> float Incr.t = <fun>
```

And let's get a starting set of input values.


```ocaml non-deterministic
# let inputs = Array.init 10_000 ~f:(fun i -> Incr.Var.create (Float.of_int i));;
val inputs : float Incr.Var.t array =
  [|<abstr>; <abstr>; <abstr>; ...|]
# let (:=) = Incr.Var.set
val ( := ) : 'a Incr.Var.t -> 'a -> unit = <fun>
# let (!) = Incr.Var.value;;
val ( ! ) : 'a Incr.Var.t -> 'a = <fun>
```

Now, let's also create an incremental that dynamically chooses which
values to sum together.  Here, we'll initialize it to indices of the
first 100 elements.

```ocaml
# let things_to_sum = Incr.Var.create (List.init ~f:Fn.id 100)
val things_to_sum : int list Incr.Var.t = <abstr>
```

Now we can use `bind` to build a computation that sums together the
elements from `inputs` as indicated by `things_to_sum`.

```ocaml
# let dynamic_sum =
    let%bind things_to_sum = Incr.Var.watch things_to_sum in
    incr_list_sum
      (List.map things_to_sum ~f:(fun i ->
         Incr.Var.watch inputs.(i)))
val dynamic_sum : float Incr.t = <abstr>
```

Now, let's observe the value and write some code to stabilize the
computation and print out the results.

```ocaml
# let dynamic_sum_obs = Incr.observe dynamic_sum
val dynamic_sum_obs : float Incr.Observer.t = <abstr>
# let print () =
    Incr.stabilize ();
    printf "%f\n" (Incr.Observer.value_exn dynamic_sum_obs);;
val print : unit -> unit = <fun>
# let () = print ()
4950.000000
```

Now we can update the computation by either changing the values in the
`inputs` array, or by changing `things_to_sum`.

```ocaml
# let () =
    inputs.(3) := !(inputs.(3)) +. 50.;
    print ()
5000.000000
# let () =
    things_to_sum := [1;10;100];
    print ()
111.000000
```

# Incremental performance, and why you often want to avoid bind

We've now used incremental to build a computation that dynamically
changes its structure. But let's stop for a moment to think about the
performance characteristics of this version of the computation.  After
all, incremental is an optimization framework! If you're not thinking
about performance, you probably shouldn't be using incremental at all.

In particular, what is the /incremental performance/ of `dynamic_sum`?
I.e., what is the computational cost of stabilizing the computation
after the inputs change?

Generally speaking, the incremental performance of a computation
depends on the nature of the change to its inputs. In this specific
case, there are two simple cases worth thinking about: the cost of a
change to the `inputs` array, and a change to `things_to_sum`.

When the `inputs` array change, the cost is logarithmic in the size of
the summation tree, i.e., logarithmic in the length of
`things_to_sum`.

When `things_to_sum` itself changes, however, you'll find the cost is
quite a lot larger.  Every time `things_to_sum` changes, the `bind` in
the computation of `dynamic_sum` fires, and we have to rerun
`incr_list_sum`.  That means we have to rebuild the summation tree
from scratch, the cost of which is linear in the length of
`things_to_sum`.

This highlights an important fact: *`bind` is often expensive*. In
particular, if you make large parts of your graph dynamic by dint of
using `bind`, you tend to make that part of the computation entirely
non-incremental.  A common anti-pattern people trip over when writing
incremental code is to use `bind` in places where they should be using
`map`, thereby destroying the incremental performance of their
programs. (More on this later, in [Part 4:
Pitfalls](./part4-pitfalls.mdx).)

In the next part, we'll discuss other techniques for building dynamic,
incremental computations.

[Part 3: Incr\_map](./part3-map.mdx)
